start:
	say "Salut je suis ta conseillère musicale 👩‍🎤, je connais tous les groupes de musique !"
  // Les Typing() indique au chatbot qu'il doit faire semblant de taper 😉 On met le nombre de milliseconde entre parenthese
  say Typing(5000)
  say Image("https://media.giphy.com/media/dM2xuxnJCg4H6/giphy.gif")
  say Typing(3000)
  goto name

name:
  // Ci dessous on va vérfier si la variable `name` existe
  // si c'est le cas, pas la peine de poser la question et on passe directement à l'étape suivante
  if (name) {
    goto which_artist
  }
  say Question("Avant tout faisons connaissance, comment tu t'appelles ?",
    buttons=[
      Button("Je suis anonyme 🏴‍☠️") as btnanon
    ])
  hold
  if (event match btnanon) {
    say "Ok, ok, pas de soucis, on peut quand même bien s'entendre 👊"
    remember name = "Anon"
  } else {
    remember name = event
  }
  goto which_artist

which_artist:
say Typing(5000)
	say Question("Quel.le artiste aimes-tu ? Voici quelques exemples :",
	buttons=[
		Button("Billie Eilish 👩‍🦰") as btnbillie,
		Button("Eminem 👌") as btneminem,
		Button("Nina Simone 🎷") as btnnina
		])
	hold
	if (event match btnbillie ) {
		remember artist = "Billie Eilish"
	} else if (event match btneminem) {
		remember artist = "eminem"
    
	} else if ( event match btnnina ) {
    remember artist = "nina simone"
  } else {
    remember artist = event
  }
  goto display

display:
  say Typing(5000)
  do res = Fn("Similar", query= artist )
  do cards = []
  if (!res || res == {} || res.length() == 0 || res.success == false) {
    say "Je n'ai pas trouvé d'artiste', désolé 😔"
    goto which_artist
  }
  say "Voici quelques artistes similaires 🤩"
  foreach (val, index) in res {
    say "{{index + 1}}: {{val.name}}"
    say Video(val.youtube)
    say Typing(5000)
    if (index >= 2) goto end
  }
  